<?xml version="1.0" encoding="UTF-8"?>
<Export generator="Cache" version="25">
<Class name="DocBook.refentry">
<Description>
Represents a reference entry.</Description>
<ClassType>persistent</ClassType>
<IncludeCode>DocBookMac</IncludeCode>
<ProcedureBlock>1</ProcedureBlock>
<Super>DocBook.component</Super>
<TimeCreated>58696,41768</TimeCreated>

<Property name="refmeta">
<Type>refmeta</Type>
</Property>

<Method name="GetGotoList">
<Internal>1</Internal>
<FormalSpec><![CDATA[&gtl:cdata]]></FormalSpec>
<Implementation><![CDATA[
	Set id = ..%Id()

	// list all of the sect1 blocks
	&sql(DECLARE G1Cursor CURSOR FOR
		SELECT ID,blockid, title
		INTO :sid, :key, :title
		FROM refsect1
		WHERE container = :id
	)

	&sql(OPEN G1Cursor)
	&sql(FETCH G1Cursor)

	Set num = 1
	While (SQLCODE = 0) {
		Set gtl(num) = $LB("#" _ key,title)
		&sql(FETCH G1Cursor)
		Set num = num + 1
	}

	&sql(CLOSE G1Cursor)
	
	Quit
]]></Implementation>
</Method>

<Method name="GetBannerInfo">
<Internal>1</Internal>
<FormalSpec>info:DocBook.UI.BannerInfo</FormalSpec>
<Implementation><![CDATA[
	Set info.title = ..refmeta.refentrytitle
	If (..book '= "") {
		Set info.booktitle = ..book.title
	}
	Else {
		Set info.booktitle = ""
	}		

	If (..container = "") {
		Set info.chap = ""
		Quit
	}
	
	Set info.chap = ..container.title
	
	// find prev/next chapter

	If (..container = "") {
		Quit
	}
	Set cid = ..container.%Id()
	Set pos = ..blockpos
	
	&sql(SELECT blockid INTO :prev
		FROM refentry 
		WHERE (container = :cid) AND (blockpos < :pos)
		ORDER BY blockpos DESC)
		
	If (SQLCODE = 0) {
		Set info.prev = prev
	}
		
	&sql(SELECT blockid INTO :next
		FROM refentry 
		WHERE (container = :cid) AND (blockpos > :pos)
		ORDER BY blockpos)
		
	If (SQLCODE = 0) {
		Set info.next = next
	}
]]></Implementation>
</Method>

<Method name="XMLRenderPreChildren">
<Description>
render any XML before children</Description>
<Internal>1</Internal>
<Implementation><![CDATA[
	// assume class name is XML element name

	Set attr = ""
	If (..id '= "") {
		Set attr = attr _ " id=""" _ ..id _ """"
	}
	If (..role '= "") {
		Set attr = attr _ " role=""" _ ..role _ """"
	}
	
	Write "<",..%ClassName(),attr,">",!

	Write "<refmeta>",!
	If (..refmeta.refentrytitle '= "") {
		Write "<refentrytitle>", ..refmeta.refentrytitle, "</refentrytitle>",!
	}
	Write "</refmeta>",!
]]></Implementation>
</Method>

<Storage name="Default">
<Type>%Library.CacheStorage</Type>
<DefaultData>refentryDefaultData</DefaultData>
<Data name="refentryDefaultData">
<Structure>listnode</Structure>
<Subscript>"refentry"</Subscript>
<Value name="1">
<Value>refmeta</Value>
</Value>
</Data>
</Storage>
</Class>
</Export>
